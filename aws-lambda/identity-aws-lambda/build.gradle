configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
    compile project(':core:identity-core')
    compile project(':dao:identity:identity-dao-dynamodb')
    compile project(':json-api:identity-json-api')
    compile project(':plugins:identity:alias-loader')
    compile project(':aws-lambda')

    compile 'com.amazonaws:aws-lambda-java-core:1.2.0'

    testCompile project(':dao:identity:identity-dao-fake')
    testCompile 'com.github.stefanbirkner:system-rules:1.19.0'
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip

task integrationTest(type: Test) {
    systemProperty 'sqlite4java.library.path', '../../libs/sqlite4java'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test