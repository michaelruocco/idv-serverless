configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
    compile project(':aws-lambda')
    compile project(':json-api:lockout-decision-json-api')

    integrationTestCompile project(':dao:lockout-decision:lockout-decision-dao-fake')
    integrationTestCompile project(':dao:identity:identity-dao-fake')
    integrationTestCompile 'org.skyscreamer:jsonassert:1.5.0'
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test