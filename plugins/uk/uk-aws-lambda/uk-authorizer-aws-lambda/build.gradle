configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
    compile project(':aws-lambda:authorizer-aws-lambda')
    compile 'com.github.michaelruocco:file-loader:4.0.0'

    integrationTestCompile 'com.github.stefanbirkner:system-rules:1.19.0'
    integrationTestCompile 'org.skyscreamer:jsonassert:1.5.0'
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip

task integrationTest(type: Test) {
    systemProperty 'sqlite4java.library.path', '../../libs/sqlite4java'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test